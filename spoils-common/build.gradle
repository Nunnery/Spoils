plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id "com.github.johnrengelman.shadow" version "1.2.0"
}

group = "me.topplethenun"
version = "0.0.1-SNAPSHOT"

sourceCompatibility = 1.7

dependencies {
    compile("org.spigotmc:spigot-api:1.8-R0.1-SNAPSHOT") {
        exclude group: "net.md-5"
    }
    compile "me.topplethenun:hilt:latest.integration"
    compile "me.topplethenun:config:latest.integration"
}

license {
    header file('HEADER')
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

artifacts {
    archives shadowJar
}

publishing {
    publications {
        java(MavenPublication) {
            artifactId = "spoils-common"
            from components.shadow
        }
    }
    repositories {
        maven {
            url "http://repo-topplethenun.rhcloud.com/nexus/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
            name "nunnerycode-repo"
            credentials {
                username System.getenv("nexusUsername") ?: nexusUsername
                password System.getenv("nexusPassword") ?: nexusPassword
            }
        }
    }
}

shadowJar {
    dependencies {
        include(dependency("me.topplethenun:config:"))
        include(dependency("me.topplethenun:hilt"))
    }

    relocate "me.topplethenun.config", "me.topplethenun.spoils.config"
    relocate "me.topplethenun.hilt", "me.topplethenun.spoils.hilt"
}
