import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id "com.github.johnrengelman.shadow" version "1.2.0"
}

group = "com.tealcube.minecraft.spigot"
version = "0.0.2-SNAPSHOT"

sourceCompatibility = 1.7

dependencies {
    compile project(":spoils-common")
    compile project(":spoils-api")
}

processResources {
    filter ReplaceTokens, tokens: [
            VERSION: version + "-" + (System.getenv("BUILD_NUMBER") ?: "0"),
            NAME   : System.getenv("JOB_NAME") ?: project.name
    ]
}

artifacts {
    archives shadowJar
}

license {
    header file('HEADER')
    exclude "**/*.txt"
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        shadow(MavenPublication) {
            artifactId = "spoils-core"
            from components.shadow
        }
    }
    repositories {
        maven {
            url "http://tealcube.com/nexus/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
            name "TealCube"
            credentials {
                username System.getenv("nexusUsername") ?: nexusUsername
                password System.getenv("nexusPassword") ?: nexusPassword
            }
        }
    }
}

shadowJar {
    dependencies {
        include project(path: ":spoils-common", configuration: "shadow")
        include project(path: ":spoils-api", configuration: "shadow")
    }
}