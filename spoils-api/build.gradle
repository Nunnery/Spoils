plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id "com.github.johnrengelman.shadow" version "1.2.0"
}

group = "com.tealcube.minecraft.spigot"
version = "0.0.1-SNAPSHOT"

sourceCompatibility = 1.7

dependencies {
    compile project(":spoils-common")
    compile "se.ranzdo.bukkit:methodcommand:0.2-SNAPSHOT"
}

license {
    header file('HEADER')
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        java(MavenPublication) {
            artifactId = "spoils-api"
            from components.java
        }
    }
    repositories {
        maven {
            url "http://tealcube.com/nexus/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
            name "TealCube"
            credentials {
                username System.getenv("nexusUsername") ?: nexusUsername
                password System.getenv("nexusPassword") ?: nexusPassword
            }
        }
    }
}

shadowJar {
    classifier ""

    dependencies {
        exclude(project(":spoils-common"))
    }
}