import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java"
    id "maven-publish"
    id "com.github.hierynomus.license" version "0.11.0"
}

group = "me.topplethenun"
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
    maven {
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
}

dependencies {
    compile ("org.spigotmc:spigot-api:1.8-R0.1-SNAPSHOT") {
        exclude group: "net.md-5"
    }
    testCompile group: "junit", name: "junit", version: "4.11"
}

processResources {
    filter ReplaceTokens, tokens: [
            VERSION: version + "-" + (System.getenv("CIRCLECI") != null ? "circleci" : "compiled") + "-" + (System.getenv("CIRCLE_BUILD_NUM") ?: "0"),
            NAME   : project.name
    ]
}

license {
    header file('HEADER')
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        java(MavenPublication) {
            artifactId = "spoils"
            from components.java
        }
    }
    repositories {
        maven {
            url "http://repo-topplethenun.rhcloud.com/nexus/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
            name "nunnerycode-repo"
            credentials {
                username System.getenv("nexusUsername") ?: nexusUsername
                password System.getenv("nexusPassword") ?: nexusPassword
            }
        }
    }
}